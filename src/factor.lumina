import "../std/io.lumina"
import "../std/collections.lumina"

import "compiler.lumina"
import "parser.lumina"
import "lexer.lumina"
import "type.lumina"
import "block.lumina"
import "position.lumina"
import "operator.lumina"
import "bitwise.lumina"


type FactorPart {
	Operator operator;
	Bitwise value;
}

type Factor {
	Region region;

	Bitwise value;
	Array<FactorPart> parts;

	func parseFactor(Factor self, Parser parser, Lexer lexer, Block block) -> Factor {
		self.region = lexer.startRegion();

		self.value = new Bitwise();
		self.value.parseBitwise(parser, lexer, block);

		self.parts = createArray<FactorPart>();

		while (lexer.current.operator.precedence == PREC_factor) {
			var part = new FactorPart();
			part.operator = lexer.consumePrecedence(PREC_factor);
			
			part.value = new Bitwise();
			part.value.parseBitwise(parser, lexer, block);

			self.parts.push(part);
		}

		lexer.endRegion(self.region);

		return self;
	}

	func bindFactor(Factor self, Parser parser) {
		self.value.bindBitwise(parser);

		var i = 0;
		while (i < self.parts.size) {
			var part = self.parts.get(i);
			part.value.bindBitwise(parser);
			i = i + 1;
		}
	}

	func typeCheckFactor(Factor self, Parser parser) -> TypeObj {
		var leftType = self.value.typeCheckBitwise(parser);

		if (self.parts.size == 0) {
			return leftType;
		}

		if (!leftType.checkType(parser.int, parser)) {
			parser.error(self.region.start, " expected left hand side of factor operation to be of type 'int'\n");
		}

		var i = 0;
		while (i < self.parts.size) {
			var part = self.parts.get(i);
			var rightType = part.value.typeCheckBitwise(parser);

			if (!rightType.checkType(parser.int, parser)) {
				parser.error(part.value.region.start, " expected right hand side of factor operation to be of type 'int'\n");
			}

			i = i + 1;
		}

		return parser.int;
	}

	func writeFactor(Factor self, Compiler compiler, bool getPointer) -> Register {
		var valueRegister = self.value.writeBitwise(compiler, getPointer);

		var i = 0;
		while (i < self.parts.size) {
			print("can't write factor operation\n");
			i = i + 1;
		}

		return valueRegister;
	}

	func getTypeFactor(Factor self, Parser parser) -> TypeObj {
		return self.value.getTypeBitwise(parser);
	}
}
