import "../std/collections.lumina"

import "parser.lumina"
import "lexer.lumina"
import "position.lumina"
import "condition.lumina"
import "name.lumina"
import "expression.lumina"
import "operator.lumina"
import "block.lumina"


type Else {
	Region region;

	Name keyword;
	Block block;

	func parseElse(Else self, Parser parser, Lexer lexer, SyntaxTree tree, Name elseKeyword) -> Else {
		self.region = lexer.startRegion();

		self.keyword = elseKeyword;
		self.block = new Block();
		self.block.parseBlock(parser, lexer, tree);

		lexer.endRegion(self.region);
		return self;
	}
}

type ElseIf {
	Region region;

	Name elseKeyword;
	Name ifKeyword;
	Condition condition;
	Block block;

	int id;

	func parseElseIf(ElseIf self, Parser parser, Lexer lexer, SyntaxTree tree, Name elseKeyword) -> ElseIf {
		self.region = lexer.startRegion();

		self.elseKeyword = elseKeyword;
		self.ifKeyword = lexer.consumeKeyword(KEYWORD_if);

		self.condition = new Condition();
		self.condition.parseCondition(parser, lexer);

		self.block = new Block();
		self.block.parseBlock(parser, lexer, tree);

		lexer.endRegion(self.region);
		return self;
	}
}

type If {
	Region region;
	Name keyword;
	Condition condition;
	Block block;
	Array<ElseIf> elseIfs;
	Else elseObj;

	int id;

	func parseIf(If self, Parser parser, SyntaxTree tree, Lexer lexer) -> If {
		self.region = lexer.startRegion();

		self.keyword = lexer.consumeKeyword(KEYWORD_if);

		self.condition = new Condition();
		self.condition.parseCondition(parser, lexer);

		self.block = new Block();
		self.block.parseBlock(parser, lexer, tree);

		self.elseIfs = createArray<ElseIf>();

		var elseKeyword = lexer.consumeKeyword(KEYWORD_else);
		while (elseKeyword != (Name)0) {
			if (isKeyword(lexer.current.name, KEYWORD_if)) {
				var elseIf = new ElseIf();
				elseIf.parseElseIf(parser, lexer, tree, elseKeyword);

				self.elseIfs.push(elseIf);
			} else {
				self.elseObj = new Else();
				self.elseObj.parseElse(parser, lexer, tree, elseKeyword);
			}

			elseKeyword = lexer.consumeKeyword(KEYWORD_else);
		}

		lexer.endRegion(self.region);
		return self;
	}
}
