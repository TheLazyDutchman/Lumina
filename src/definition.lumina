import "../std/string.lumina"
import "../std/collections.lumina"

import "name.lumina"
import "position.lumina"
import "whitespace.lumina"
import "type.lumina"
import "variable.lumina"
import "function.lumina"


type Definition {
	str name;

	TypeObj typeObj;
	Variable variable;
	Function function;

	Definition listDefinition;

	func createListDefinition(Definition self) -> Definition {
		if (self.typeObj == (TypeObj)0) {
			return (Definition)0;
		}

		if (self.listDefinition == (Definition)0) {
			self.listDefinition = createDefinitionFromType(createListType((str)0, self.typeObj));
		}

		return self.listDefinition;
	}

	func getVariantDefinition(Definition self, Array<TypeObj> generics) -> Definition {
		if (self.typeObj != (TypeObj)0) {
			return createDefinitionFromType(self.typeObj.getVariantType(generics));
		}

		if (self.function != (Function)0) {
			return createDefinitionFromFunction(self.function.getVariantFunction(generics));
		}

		return (Definition)0;
	}
}

func createDefinitionFromType(TypeObj typeObj) -> Definition {
	var definition = new Definition();

	definition.name = typeObj.name.name;
	definition.typeObj = typeObj;

	return definition;
}

func createDefinitionFromVariable(Variable variable) -> Definition {
	var definition = new Definition();

	definition.name = variable.name.name;
	definition.variable = variable;

	return definition;
}

func createDefinitionFromFunction(Function function) -> Definition {
	var definition = new Definition();

	definition.name = function.name.name;
	definition.function = function;

	return definition;
}

func findDefinition(Array<Definition> list, str name) -> Definition {
	var i = 0;
	while (i < list.size) {
		if (streq(list.get(i).name, name)) {
			return list.get(i);
		}
		i = i + 1;
	}
	return (Definition)0;
}
