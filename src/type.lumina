import "../std/string.lumina"
import "../std/io.lumina"
import "../std/collections.lumina"

import "parser.lumina"
import "lexer.lumina"
import "definition.lumina"
import "identifier.lumina"
import "index.lumina"
import "name.lumina" 
import "function.lumina"
import "operator.lumina"
import "operation.lumina"
import "position.lumina"
import "whitespace.lumina"
import "block.lumina"
import "variable.lumina"


type Property {
	Region region;

	Identifier propertyType;
	Name name;
	Operator semicolon;

	int offset;
	int nameId;
	TypeObj typeObj;

	func parseProperty(Property self, Parser parser, Lexer lexer, Block block, TypeObj typeObj) -> Property {
		self.typeObj = typeObj;
		self.region = lexer.startRegion();

		self.propertyType = new Identifier();
		self.propertyType.parseIdentifier(parser, lexer, block);

		self.name = lexer.consumeIdentifier();
		if (self.name == (Name)0) {
			parser.error(lexer.here(), " expected property name\n");
		}

		self.semicolon = lexer.consumeOperator(";");
		if (self.semicolon == (Operator)0) {
			parser.error(lexer.here(), " expected ';' after property\n");
		}

		lexer.endRegion(self.region);
		return self;
	}
	
	func bindProperty(Property self, Parser parser) {
		if (findProperty(self.typeObj.properties, self.name.name) != self) {
			parser.error(self.region.start, " a property with this name already exists\n");
		}

		if (findFunction(self.typeObj.methods, self.name.name) != (Function)0) {
			parser.error(self.region.start, " a method with this name already exists\n");
		}

		self.propertyType.bindIdentifier(parser);
	}
}

type TypeObj {
	Region region;
	Name keyword;
	Identifier name;
	int size;
	int objectSize;

	Operator lbrace;

	Array<Property> properties;
	Array<Function> methods;

	Operator rbrace;

	TypeObj listOf;
	TypeObj listType;

	bool isGeneric;

	Block block;
	int nameId;
	

	func parseType(TypeObj self, Parser parser, SyntaxTree tree, Lexer lexer, Block block) -> TypeObj {
		self.block = block;
		self.region = lexer.startRegion();

		self.keyword = lexer.consumeKeyword(KEYWORD_type);

		self.name = new Identifier();
		self.name.parseIdentifier(parser, lexer, block);

		self.lbrace = lexer.consumeOperator("{");
		if (self.lbrace == (Operator)0) {
			parser.error(lexer.here(), " expected '{' before type properties\n");
		}

		self.properties = createArray<Property>();
		self.methods = createArray<Function>();

		while (!isOperator(lexer.current.operator, "}") && !lexer.current.isEOF) {
			if (isKeyword(lexer.current.name, KEYWORD_func)) {
				var method = new Function();
				method.parseFunction(parser, tree, lexer, block);
				self.methods.push(method);
			} else {
				var property = new Property();
				property.parseProperty(parser, lexer, block, self);
				self.properties.push(property);
			}
		}

		self.rbrace = lexer.consumeOperator("}");
		if (self.rbrace == (Operator)0) {
			parser.error(lexer.here(), " expected '}' after type definition\n");
		}

		lexer.endRegion(self.region);
		return self;
	}

	func bindType(TypeObj self, Parser parser) {
		var definition = self.block.findDefinitionInBlock(self.name.name);

		if (definition.typeObj != self) {
			parser.error(self.region.start, " a type with this name already exists\n");
			print("[NOTE] the other is defined here: ");
			printPosition(definition.typeObj.region.start);
			print("\n");
		}

		if (definition.variable != (Variable)0) {
			parser.error(self.region.start, " a variable with this name already exists\n");
			print("[NOTE] the other is defined here: ");
			printPosition(definition.variable.region.start);
			print("\n");
		}

		if (definition.function != (Function)0) {
			parser.error(self.region.start, " a function with this name already exists\n");
			print("[NOTE] the other is defined here: ");
			printPosition(definition.function.region.start);
			print("\n");
		}

		var i = 0;
		while (i < self.properties.size) {
			var property = self.properties.get(i);
			property.bindProperty(parser);
			i = i + 1;
		}

		i = 0;
		while (i < self.methods.size) {
			var method = self.methods.get(i);
			if (findFunction(self.methods, method.name.name) != method) {
				parser.error(method.region.start, " a method with this name already exists\n");
			}

			if (findProperty(self.properties, method.name.name) != (Property)0) {
				parser.error(method.region.start, " a property with this name already exists\n");
			}

			method.bindFunction(parser);
			i = i + 1;
		}
	}
}

func findProperty(Array<Property> list, str name) -> Property {
	var i = 0;
	while (i < list.size) {
		if (streq(list.list[i].name.name, name)) {
			return list.list[i];
		}
		i = i + 1;
	}
	return (Property)0;
}

func createPropertyFromString(str name, TypeObj propertyType, int offset) -> Property {
	var property = new Property();

	property.name = createNameFromString(name);
	property.propertyType = propertyType.name;

	property.offset = offset;

	return property;
}

func createTypeFromString(str name, int size) -> TypeObj {
	var typeObj = new TypeObj();

	typeObj.name = createIdentifierFromString(name);
	typeObj.size = size;
	typeObj.objectSize = size;

	typeObj.properties = createArray<Property>();

//	typeObj.variants = createArray<TypeObj>();
//	typeObj.variants.push(typeObj);

	return typeObj;
}

func createListType(Array<TypeObj> typeArray, str name, TypeObj typeObj) -> TypeObj {
	if (typeObj.listOf != (TypeObj)0) {
		return typeObj.listOf;
	}

	var listType = createTypeFromString(name, 8);
	listType.objectSize = typeObj.size;
	listType.listType = typeObj;
	listType.isGeneric = typeObj.isGeneric;

	typeObj.listOf = listType;

	if (typeArray != (Array<TypeObj>)0) {
		typeArray.push(listType);
	}

	return listType;
}

func findType(Array<TypeObj> list, str name) -> TypeObj {
	var i = 0;
	while (i < list.size) {
		var item = list.get(i);
		if (item.name.name != (str)0 && streq(item.name.name, name)) {
			return list.list[i];
		}
		i = i + 1;
	}
	return (TypeObj)0;
}
//
//func printType(TypeObj typeObj) {
//	if (typeObj == (TypeObj)0) {
//		print("type is null");
//		return;
//	}
//
//	if (typeObj.generics.size != 0) {
//		print(typeObj.name);
//		print("<");
//		var i = 0;
//		while (i < typeObj.generics.size) {
//			if (i != 0) {
//				print(",");
//			}
//			printType(typeObj.generics.list[i].typeObj);
//			i = i + 1;
//		}
//		print(">");
//		return;
//	}
//
//	if (typeObj.listType != (TypeObj)0 && typeObj.name == (str)0) {
//		printType(typeObj.listType);
//		print("[]");
//		return;
//	}
//
//	if (typeObj.name == (str)0) {
//		print("type name is null");
//		return;
//	}
//
//	print(typeObj.name);
//}
//
//func printTypeArray(Array<TypeObj> list) {
//	var i = 0;
//	print("[");
//	while (i < list.size) {
//		if (i != 0) {
//			print(",");
//		}
//		printType(list.list[i]);
//		i = i + 1;
//	}
//	print("]");
//}
