import "collections.lumina"
import "io.lumina"


const String = Array<char>;


func toString(str value) -> String {
	var string = createArray<char>();

	var i = 0;
	while (value[i] != '\0') {
		addItem<char>(string, value[i]);
		i = i + 1;
	}

	return string;
}

func copyPart(String original, int offset, int size) -> String {
	var string = createArray<char>();

	var i = 0;
	while (i < size) {
		addItem<char>(string, getItem<char>(original, offset + i));
		i = i + 1;
	}

	return string;
}

func split(String string, str separator) -> Array<String> {
	var array = createArray<String>();
	var separatorSize = strlen(separator);
	var index = 0;

	var offset = 0;
	var size = match(string, separator, offset);

	while (size != -1) {
		var part = copyPart(string, offset, size);
		addItem<String>(array, part);

		offset = offset + size + separatorSize;
		size = match(string, separator, offset);
	}

	var part = copyPart(string, offset, string.size - offset);
	addItem<String>(array, part);

	return array;
}

func join(Array<String> strings, str separator) -> String {
	var string = createArray<char>();

	var i = 0;
	while (i < strings.size) {
		if (i != 0) {
			var j = 0;
			while(separator[j] != '\0') {
				addItem<char>(string, separator[j]);
				j = j + 1;
			}
		}

		extendArray<char>(string, getItem<String>(strings, i));
		i = i + 1;
	}

	return string;
}

func match(String string, str separator, int offset) -> int {
	var i = offset;
	while (i < string.size) {
		var isMatching = true;

		var j = 0;
		while (separator[j] != '\0') {
			if (getItem<char>(string, i + j) != separator[j]) {
				isMatching = false;
			}
			j = j + 1;
		}

		if (isMatching) {
			return i - offset;
		}

		i = i + 1;
	}

	return -1;
}

func strlen(str string) -> int{
	var i = 0;
	while (string[i] > '\0') {
		i = i + 1;
	}
	return i;
}

func strneq(str s1, str s2, int n) -> bool {
	var i = 0;

	while (i < n) {
		if (s1[i] != s2[i]) {
			return false;
		}
		i = i + 1;
	}

	return true;
}

func streq(str s1, str s2) -> bool {
	var len = strlen(s1);
	if (len != strlen(s2)) {
		return false;
	}

	return strneq(s1, s2, len);
}

func countchr(str s, char delim) -> int {
	var sum = 0;
	var i = 0;
	while (s[i] != '\0') {
		if (s[i] == delim) {
			sum = sum + 1;
		}
		i = i + 1;
	}
	return sum;
}

func strindex(str s, char delim) -> int {
	var i = 0;
	while (s[i] != delim) {
		if (s[i] == '\0') {
			return -1;
		}

		i = i + 1;
	}
	return i + 1;
}

func strncpy(str src, str dst, int count) {
	// it is currently assumed that src and dst are longer than count

	var i = 0;
	while (i < count) {
		dst[i] = src[i];
		i = i + 1;
	}
}

func strndup(str s, int count) -> str {
	var value = new str(count + 1);

	var i = 0;
	while (i < count) {
		value[i] = s[i];
		i = i + 1;
	}

	value[count] = '\0';

	return value;
}

func strdup(str s) -> str {
	var length = strlen(s);
	return strndup(s, length);
}

func isalpha(char c) -> bool {
	return ('a' <= c && c <= 'z') || ('A' <= c && c <= 'Z');
}

func isnumeric(char c) -> bool {
	return '0' <= c && c <= '9';
}

func isalnum(char c) -> bool {
	return isalpha(c) || isnumeric(c);
}

func isspace(char c) -> bool {
	return c == ' ' || c == '\t' || c == '\n';
}

func intToString(int number) -> str {
	var buffer = new str(22); //enough size for any number
	var isNegative = number < 0;
	if (isNegative) {
		number = -number;
		buffer[0] = '-';
	}

	var index = (int)isNegative;
	var nextSize = 1;

	while (nextSize * 10 <= number) {
		nextSize = nextSize * 10;
	}

	while (nextSize > 1) {
		var size = 1;

		while (size * 10 < nextSize) {
			size = size * 10;
		}
		nextSize = size;
		size = size * 10;

		var counter = 0;
		
		while (size <= number) {
			number = number - size;
			counter = counter + 1;
		}

		buffer[index] = '0' + counter;

		index = index + 1;
	}

	buffer[index] = '0' + number; // the last loop leaves the last digit
	buffer[index + 1] = '\0';

	return buffer;
}

func strToInt(str string, int len) -> int{
	var value = 0;
	var i = 0;

	while (i < len) {
		if (isnumeric(string[i]) == false) {
			return value;
		}

		value = value * 10;

		value = value + string[i] - '0';
		
		i = i + 1;
	}

	return value;
}

func unEscapeCharacter(str charPtr) -> char {
	var chr = charPtr[0];

	if (chr == '\\') {

		var test = charPtr[1];

		if (test == 'a') {
			chr = '\a';
		} else if (test == 'b') {
			chr = '\b';
		} else if (test == 'e') {
			chr = '\e';
		} else if (test == 'f') {
			chr = '\f';
		} else if (test == 'n') {
			chr = '\n';
		} else if (test == 'r') {
			chr = '\r';
		} else if (test == 't') {
			chr = '\t';
		} else if (test == 'v') {
			chr = '\v';
		} else if (test == '\\') {
			chr = '\\';
		} else if (test == '\'') {
			chr = '\'';
		} else if (test == '\"') {
			chr = '\"';
		} else if (test == '?') {
			chr = '\?';
		} else if (test == '0') { // cannot do octal yet, just 0
			chr = '\0';
		} else {
			chr = test;
		}
	}
	return chr;
}
