import "string.lumina"
import "environment.lumina"

func fprintn(int fd, str string, int length) -> int {
	var SYSWRITE = 1;
	return (int)syscall(SYSWRITE, fd, string, length, 0, 0, 0);
}

func fprint(int fd, str string) -> int {
	var length = strlen(string);
	return fprintn(fd, string, length);
}

var STDOUT = 1;

func printn(str string, int length) -> int {
	return fprintn(STDOUT, string, length);
}

func print(str string) -> int {
	return fprint(STDOUT, string);
}

func fprinti(int file, int number) {
	var buffer = (str)malloc(22); //enough size for any number
	var isNegative = number < 0;
	if (isNegative) {
		number = (!number + 1); //two's complement
		buffer[0] = "-";
	}

	var index = (int)isNegative;
	var nextSize = 1;

	while (nextSize * 10 <= number) {
		nextSize = nextSize * 10;
	}

	while (nextSize > 1) {
		var size = 1;

		while (size * 10 < nextSize) {
			size = size * 10;
		}
		nextSize = size;
		size = size * 10;

		var counter = 0;
		
		while (size <= number) {
			number = number - size;
			counter = counter + 1;
		}

		buffer[index] = "0" + counter;

		index = index + 1;
	}

	buffer[index] = "0" + number; // the last loop leaves the last digit
	buffer[index + 1] = "\0";

	fprint(file, buffer);

	free((ptr)buffer);
}

func printi(int number) {
	fprinti(STDOUT, number);
}

func execvp(str fileName, str[] args) -> int {
	var path = getEnvironmentVariable("PATH");
	var numPaths = countchr(path, ":") + 2;
	var paths = (str[])malloc(numPaths * 8); // ptr size is 8

	var index = strindex(path, ":");
	var i = 0;

	while (index > 0) {
		paths[i] = strndup(path, index - 1);
		path = (str)((int)path + index);
		index = strindex(path, ":");
		i = i + 1;
	}

	paths[i] = strdup(path);
	paths[numPaths - 1] = (str)0;

	i = 0;

	var dir = paths[0];
	while (dir != (str)0) {
		var dir_fd = open(paths[i], 2162688, 0);

		if (dir_fd < 0) {
			print("[ERROR] could not open dir '");
			print(paths[i]);
			print("', error code: ");
			printi(-dir_fd);
			print(" \n");

			return -1;
		}

		var file_fd = openat(dir_fd, fileName, 2097152, 0);
		if (file_fd > 0) {
			var length = strlen(dir);
			var hasNoSlash = (bool)0;

			if (dir[length] != '/') {
				length = length + 1;
				hasNoSlash = (bool)1;
			}

			length = length + strlen(fileName);

			var buffer = (str)malloc(length + 1);
			var fullPath = buffer;

			strncpy(dir, buffer, strlen(dir));
			buffer = (str)((int)buffer + strlen(dir));

			if (hasNoSlash) {
				buffer[0] = '/';
				buffer = (str)((int)buffer + 1);
			}

			strncpy(fileName, buffer, strlen(fileName));

			buffer[length] = '\0';

			return execve(fullPath, args, (str[])0);
		}

		i = i + 1;
		dir = paths[i];
	}

	return -2;
}

func WIFEXITED(int status) -> bool {
	return status & 127 == 0;
}

func WIFEXITSTATUS(int status) -> int {
	var value = 0;
	while (256 <= status) {
		value = value + 1;
		status = status - 256;
	}
	return value;
}
