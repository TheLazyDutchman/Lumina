import "string.lumina"
import "io.lumina"
import "collections.lumina"


type Path {
	Array<String> folders;
	String name;
	String extension;
}


func _is_outer(String s) -> bool {
	if (s.get(0) != '.') { return false; }
	if (s.get(1) != '.') { return false; }
	return true;
}

func setFileName(Path path, String filename) {
	var nameParts = split(filename, ".");
	if (nameParts.size == 1) {
		path.name = filename;
		path.extension = (String)0;
		return;
	}

	var nameStrings = createArray<String>();
	var i = 0;
	while (i < nameParts.size - 1) {
		nameStrings.push(nameParts.get(i));
		i = i + 1;
	}

	path.name = join(nameStrings, ".");
	path.extension = nameParts.get(nameParts.size - 1);
}

func toPath(String value) -> Path {
	var parts = split(value, "/");
	var folders = createArray<String>();

	var i = 0;
	while (i < parts.size - 1) {
		var part = parts.get(i);

		if (_is_outer(part) && folders.size != 0 && !_is_outer(folders.get(folders.size - 1))) {
			folders.pop();
		} else if (!streq(part.list, ".")) {
			folders.push(part);
		}

		i = i + 1;
	}

	var path = new Path();
	path.folders = folders;

	setFileName(path, parts.get(parts.size - 1));

	return path;
}

func toLocalPath(String path, String file) -> Path {
	var outerPath = toPath(path);
	var parts = split(file, "/");

	var i = 0;
	while (i < parts.size - 1) {
		var part = parts.get(i);

		if (_is_outer(part) && outerPath.folders.size != 0 && !_is_outer(outerPath.folders.get(outerPath.folders.size - 1))) {
			outerPath.folders.pop();
		} else if (!streq(part.list, ".")) {
			outerPath.folders.push(part);
		}

		i = i + 1;
	}

	setFileName(outerPath, parts.get(parts.size - 1));

	return outerPath;
}

func pathToString(Path path) -> String {
	var string = join(path.folders, "/");

	if (path.folders.size == 0) {
		string.push('.');
	}

	string.push('/');
	string.extend(path.name);

	if (path.extension != (String)0) {
		string.push('.');
		string.extend(path.extension);
	}

	return string;
}
