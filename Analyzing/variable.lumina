import "../std/memory.lumina"
import "../std/string.lumina"

import "position.lumina"
import "name.lumina"
import "operation.lumina"
import "operator.lumina"
import "type.lumina"
import "whitespace.lumina"


type Variable {
	str name;
	VariableDefinition definition;

	int depth;
	int position;

	Type variableType;
	
	bool isConstant;
	bool isEvaluated;
	int value;
}

type VariableDefinition {
	Region region;

	Name keyword;
	Name name;
	Operator equalSign;
	Operation value;
	Operator semicolon;
	int depth;
	int position;

	Type variableType;
}

type VariableList {
	int size;
	int maxSize;
	Variable[] list;
}

func createVariableDefinition(Name keyword, Name name, Operator equalSign, Operation value, int depth, int position, Operator semicolon) -> VariableDefinition {
	var variable = (VariableDefinition)malloc(sizeof(VariableDefinition));

	variable.region = createRegion(keyword.region.start, semicolon.position);

	variable.keyword = keyword;
	variable.name = name;
	variable.equalSign = equalSign;
	variable.value = value;
	variable.semicolon = semicolon;
	variable.depth = depth;
	variable.position = position;
	variable.variableType = (Type)0;

	return variable;
}

func createVariableFromString(str name, int depth, int position, Type variableType) -> Variable {
	var variable = (Variable)malloc(sizeof(Variable));

	variable.name = name;
	variable.definition = (VariableDefinition)0;

	variable.depth = depth;
	variable.position = position;

	variable.variableType = variableType;

	variable.isConstant = (bool)0;
	variable.isEvaluated = (bool)0;
	variable.value = 0;

	return variable;
}

func createVariableFromDefinition(VariableDefinition definition) -> Variable {
	var variable = createVariableFromString(definition.name.name, definition.depth, definition.position, definition.variableType);

	variable.definition = definition;

	return variable;
}

func createVariableList() -> VariableList {
	var list = (VariableList)malloc(sizeof(VariableList));

	list.size = 0;
	list.maxSize = 8;
	list.list = (Variable[])malloc(8 * 8);

	return list;
}

func addVariable(VariableList list, Variable variable) {
	list.list[list.size] = variable;
	list.size = list.size + 1;

	if (list.size == list.maxSize) {
		list.list = (Variable[])realloc((ptr)list.list, list.maxSize * 8, list.maxSize * 8 * 2);
		list.maxSize = list.maxSize * 2;
	}
}

func findVariable(VariableList list, str name) -> Variable {
	var i = 0;
	while (i < list.size) {
		if (streq(list.list[i].name, name)) {
			return list.list[i];
		}
		i = i + 1;
	}
	return (Variable)0;
}
