import "position.lumina"
import "name.lumina"
import "operator.lumina"
import "type.lumina"


type Parameter {
	Region region;
	
	Type parameterType;
	Name name;
	Operator comma;
}

type ParameterList {
	int size;
	int maxSize;
	Parameter[] list;
}

type ReturnType {
	Region region;

	Operator rarrow;
	Type returnType;
}

type FunctionDefinition {
	Region region;

	Name keyword;
	Name name;

	Operator lparen;
	ParameterList parameters;
	Operator rparen;

	ReturnType returnType;

	int blockId;
}

type FunctionList {
	int size;
	int maxSize;
	FunctionDefinition[] list;
}

type Return {
	Region region;

	Name keyword;
	Operation expression;
	Operator semicolon;
}

func createParameter(Type parameterType, Name name, Operator comma) -> Parameter {
	var parameter = (Parameter)malloc(sizeof(Parameter));

	if (comma != (Operator)0) {
		parameter.region = createRegion(parameterType.region.start, comma.position);
	} else {
		parameter.region = createRegion(parameterType.region.start, name.region.end);
	}

	parameter.parameterType = parameterType;
	parameter.name = name;

	return parameter;
}

func createParameterList() -> ParameterList {
	var list = (ParameterList)malloc(sizeof(ParameterList));

	list.size = 0;
	list.maxSize = 8;
	list.list = (Parameter[])malloc(8 * 8);

	return list;
}

func addParameter(ParameterList list, Parameter parameter) {
	list.list[list.size] = parameter;
	list.size = list.size + 1;

	if (list.size == list.maxSize) {
		list.list = (Parameter[])realloc((ptr)list.list, 8 * list.maxSize, list.maxSize * 8 * 2);
		list.maxSize = list.maxSize * 2;
	}
}

func createReturnType(Operator rarrow, Type typeObj) -> ReturnType {
	var returnType = (ReturnType)malloc(sizeof(ReturnType));

	returnType.region = createRegion(rarrow.position, typeObj.region.end);
	
	returnType.rarrow = rarrow;
	returnType.returnType = typeObj;

	return returnType;
}

func createFunctionDefinition(Name keyword, Name name, Operator lparen, ParameterList parameters, Operator rparen, ReturnType returnType, int blockId, Position end) -> FunctionDefinition {
	var function = (FunctionDefinition)malloc(sizeof(FunctionDefinition));

	function.region = createRegion(keyword.region.start, end);

	function.keyword = keyword;
	function.name = name;
	function.lparen = lparen;
	function.parameters = parameters;
	function.rparen = rparen;
	function.returnType = returnType;
	function.blockId = blockId;

	return function;
}

func createFunctionFromString(str value) -> FunctionDefinition {
    var function = (FunctionDefinition)malloc(sizeof(FunctionDefinition));

    function.region = (Region)0;
    function.keyword = (Name)0;
    function.name = createNameFromString(value);
    function.lparen = (Operator)0;
    function.parameters = createParameterList();
    function.rparen = (Operator)0;
    function.returnType = (ReturnType)0;
    function.blockId = -1;

    return function;
}

func createFunctionList() -> FunctionList {
	var list = (FunctionList)malloc(sizeof(FunctionList));

	list.size = 0;
	list.maxSize = 8;
	list.list = (FunctionDefinition[])malloc(8 * 8);

	return list;
}

func addFunction(FunctionList list, FunctionDefinition function) {
	list.list[list.size] = function;
	list.size = list.size + 1;

	if (list.size == list.maxSize) {
		list.list = (FunctionDefinition[])realloc((ptr)list.list, list.maxSize * 8, list.maxSize * 8 * 2);
		list.maxSize = list.maxSize * 2;
	}
}

func findFunction(FunctionList list, str name) -> FunctionDefinition {
    var i = 0;
    while (i < list.size) {
	if (streq(list.list[i].name.name, name)) {
	    return list.list[i];
	}
	i = i + 1;
    }
    return (FunctionDefinition)0;
}

func createReturn(Name keyword, Operation expression, Operator semicolon) -> Return {
	var returnObj = (Return)malloc(sizeof(Return));

	returnObj.region = createRegion(keyword.region.start, semicolon.position);

	returnObj.keyword = keyword;
	returnObj.expression = expression;
	returnObj.semicolon = semicolon;

	return returnObj;
}

func createVariableFromParameter(Parameter parameter) -> VariableDefinition {
    var variable = (VariableDefinition)malloc(sizeof(VariableDefinition));

    variable.region = parameter.region;
    variable.keyword = (Name)0;
    variable.name = parameter.name;
    variable.equalSign = (Operator)0;
    variable.value = (Operation)0;
    variable.semicolon = (Operator)0;

    return variable;
}

func findVariableInParameters(FunctionDefinition function, str name) -> VariableDefinition {
    var i = 0;
    while (i < function.parameters.size) {
	if (streq(function.parameters.list[i].name.name, name)) {
	    return createVariableFromParameter(function.parameters.list[i]);
	}
	i = i + 1;
    }

    return (VariableDefinition)0;
}
